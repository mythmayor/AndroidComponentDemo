apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.mythmayor.androidcomponentdemo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.2"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), "room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    //配置keystore签名
    signingConfigs {
        releaseConfig {
            storeFile file('../test.jks')
            storePassword '123456'
            keyAlias 'test'
            keyPassword '123456'
        }
        debugConfig {
            storeFile file('../test.jks')
            storePassword '123456'
            keyAlias 'test'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            //开启混淆，删除无用代码
            minifyEnabled true
            //开启删除无用资源
            shrinkResources true
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debugConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //指定jdk版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations {
        cleanedAnnotations
        compile.exclude group: 'org.jetbrains', module: 'annotations'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.extJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoCoreVersion"

    implementation project(path: ':basic_project')
    //如果是依赖库则取消注释该行代码，如果是单独的应用程序则要注释该行代码。
    //implementation project(path: ':main_project')
    if (!isMainProjectAsApp.toBoolean()) {
        implementation project(path: ':main_project')
    }
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.ext.arouterCompiler"
    //Room持久性库 - For Kotlin use kapt instead of annotationProcessor
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
}
